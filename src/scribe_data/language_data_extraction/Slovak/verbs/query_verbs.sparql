# tool: scribe-data
# All Slovak (Q9058) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presSimp
  ?presTPS
  ?presPart
  ?pastSimp
  ?pastPart
  ?futSimp

WHERE {

  ?lexeme dct:language wd:Q9058 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .
  
  # MARK: Infinitive

  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 ;

  # MARK: Simple Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSimpForm .
    ?presSimpForm ontolex:representation ?presSimp ;
      wikibase:grammaticalFeature wd:Q3910936 ; 
      FILTER NOT EXISTS { ?presSimpForm wikibase:grammaticalFeature wd:Q51929074 . }  
      FILTER NOT EXISTS { ?presSimpForm wdt:P6191 wd:Q181970 . } 
      FILTER NOT EXISTS { ?presSimpForm wikibase:grammaticalFeature wd:Q126473 . }  
      FILTER(LANG(?presSimp) = "sk") . 
  } 

  # MARK: Third-person Singular Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
    wikibase:grammaticalFeature wd:Q51929074 ; 
    wikibase:grammaticalFeature wd:Q110786 ;   
    wikibase:grammaticalFeature wd:Q3910936 ;   
    FILTER NOT EXISTS { ?presTPSForm wdt:P6191 wd:Q181970 . }  
    FILTER NOT EXISTS { ?presTPSForm wikibase:grammaticalFeature wd:Q126473 . } 
    FILTER(LANG(?presTPS) = "sk") . 
  } 

  # MARK: Present Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presPartForm .
    ?presPartForm ontolex:representation ?presPart ;
    wikibase:grammaticalFeature wd:Q10345583 ;  
    FILTER NOT EXISTS { ?presPartForm wdt:P6191 wd:Q181970 . }  
    FILTER NOT EXISTS { ?presPartForm wikibase:grammaticalFeature wd:Q126473 . }  
    FILTER(LANG(?presPart) = "sk") . 
  } 

  # MARK: Simple Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastSimpForm .
    ?pastSimpForm ontolex:representation ?pastSimp ;
    wikibase:grammaticalFeature wd:Q1392475 ;
    FILTER NOT EXISTS { ?pastSimpForm wdt:P6191 wd:Q181970 . } 
    FILTER NOT EXISTS { ?pastSimpForm wikibase:grammaticalFeature wd:Q126473 . }  
    FILTER(LANG(?pastSimp) = "sk") .  
  } 

  # MARK: Past Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastPartForm .
    ?pastPartForm ontolex:representation ?pastPart ;
    wikibase:grammaticalFeature wd:Q1230649 ;  
    FILTER NOT EXISTS { ?pastPartForm wdt:P6191 wd:Q181970 . }  
    FILTER NOT EXISTS { ?pastPartForm wikibase:grammaticalFeature wd:Q126473 . } 
    FILTER(LANG(?pastPart) = "sk") . 
  } 

  # MARK: Future Simple

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futSimpForm .
    ?futSimpForm ontolex:representation ?futSimp ;
    wikibase:grammaticalFeature wd:Q18668199 ;  
    FILTER NOT EXISTS { ?futSimpForm wdt:P6191 wd:Q181970 . }  
    FILTER NOT EXISTS { ?futSimpForm wikibase:grammaticalFeature wd:Q126473 . } 
    FILTER(LANG(?futSimp) = "sk") . 
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
  }
}
